enable_language(CXX)

# Add all the LBANN options; this is just for the GUI, of course
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}")
include(AddLBANNOptions)

set(LBANN_CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}"
  CACHE PATH "The installation destination of LBANN")

set(LBANN_CMAKE_BUILD_TYPE "${CMAKE_BUILD_TYPE}"
  CACHE STRING "The build type for LBANN.")

set(LBANN_CMAKE_GENERATOR "${CMAKE_GENERATOR}"
  CACHE STRING "The generator used by CMake for LBANN.")

# Get the list of LBANN variables
get_property(LBANN_VARIABLES DIRECTORY PROPERTY VARIABLES)
list(FILTER LBANN_VARIABLES INCLUDE REGEX "^LBANN_.*")

create_cmake_arguments(
  OUTPUT_VARIABLE LBANN_CMAKE_ARGS
  PACKAGE_NAME LBANN
  REMOVE_PKG_NAME
  VARIABLES ${LBANN_VARIABLES})

set(LBANN_URL "https://github.com/llnl/lbann"
  CACHE STRING "The URL from which to clone LBANN")

set(LBANN_TAG "develop"
  CACHE STRING "The git tag or hash to checkout for LBANN")

# Build the dependency set
if (TARGET CNPY)
  set(_LBANN_DEPENDS "${_LBANN_DEPENDS}" CNPY)
endif ()
if (TARGET CUB)
  set(_LBANN_DEPENDS "${_LBANN_DEPENDS}" CUB)
endif ()
if (TARGET ELEMENTAL)
  set(_LBANN_DEPENDS "${_LBANN_DEPENDS}" ELEMENTAL)
endif ()
if (TARGET HYDROGEN)
  set(_LBANN_DEPENDS "${_LBANN_DEPENDS}" HYDROGEN)
endif ()
if (TARGET OPENCV)
  set(_LBANN_DEPENDS "${_LBANN_DEPENDS}" OPENCV)
endif ()
if (TARGET PROTOBUF)
  set(_LBANN_DEPENDS "${_LBANN_DEPENDS}" PROTOBUF)
endif ()

include(ExternalProject)
ExternalProject_Add(LBANN
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}
  TMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/tmp
  STAMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/stamp
  GIT_REPOSITORY ${LBANN_URL}
  GIT_TAG ${LBANN_TAG}
  DEPENDS ${_LBANN_DEPENDS}
  SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/src
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build
  INSTALL_DIR ${LBANN_CMAKE_INSTALL_PREFIX}
  CMAKE_ARGS
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  ${LBANN_CMAKE_ARGS}
  )
